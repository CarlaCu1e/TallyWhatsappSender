[#Form: Sales Color]
	
	On : Form Accept : Yes : CALL : SendWhatsapp

[Function: SendWhatsapp]

	Variable: PrintFileName    :String
	Variable: ReceiverContact  :String
	Variable: Exportedfilepath :String
	Local Formula:Wpinvoicetype:$$StringFindAndReplace:$VoucherTypeName:"\":"-"
	Local Formula:Wpinvoiceparty:$$StringFindAndReplace:$PartyLedgerName:"\":"-"
	Local Formula:Wpinvoicedate:$$String:$Date
	
    01   : Query Box:"Do you want to Whatsapp the invoice?":Yes:No
	02   : If:$$LastResult
	03   : If: NOT $$IsDirectory:"Exported PDFs"
	04   : Make Dir:"Exported PDFs"
	05   : End If
	06   : SET : ReceiverContact :$LedgerMobile:Ledger:$PartyLedgerName
	07   : If:$$IsEmpty:##ReceiverContact
	08   : Log: "Invalid party mobile number, Please enter valid mobile number in ledger"  
	09   : Return
	10   : End If
	11   : SET : PrintFileName :"Exported PDFs"+"\"+@Wpinvoicetype+"_"+@Wpinvoiceparty+"_"+@Wpinvoicedate ; Export file name
    12   : SET : SVExportFormat 	: $$SysName:PDF
	13   : SET : SVPrintFileName	: $$MakeExportName:##PrintFileName:##SVExportFormat
	14   : SET : SVOpenFileAfterExport : No
	15   : EXPORT REPORT :.: TRUE
	16   : SET :Exportedfilepath:$$GetFileFullPath:##SVPrintFileName  ;;Exported file path
    17   : Exec COM Interface:WhatsappEngine:##ReceiverContact:##Exportedfilepath:@@Msgtitle:@@waitseconds
	18   : Log:$$LastResult
	19   : End If
	20   : Return
			
		
[COM Interface:WhatsappEngine]
	
    Project:TallyWhatsappsender
	Class:Class1
	Interface:InitProcess
	Parameter:p1:String:In
	Parameter:p2:String:In
	Parameter:p3:String:In
	Parameter:p4:String:In
	Return: String
	
[System:Formulae]
	Msgtitle:"Default Title" ;; Title Message to send
	waitseconds :"3" ;; time  in seconds to wait before closing the window
 